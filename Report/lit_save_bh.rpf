{******************************************************************************
* [from Barry Hamilton]
* Module Name   : $TABLE_SAVER
*
* Purpose       : To save database tables to CSV files
*
* Document Ref. : SE/T/TVGL-WORK-INSTALL/1/13
*
* Specification : SE/T/TVGL-WORK-INSTALL/1/10
*
* Portability   : Not Checked
*
* Re-entrant    : No
*
*******************************************************************************}

SET COMPILE_OPTION DECLARE
SET NAME "DEFER/"

JOIN STANDARD_LIBRARY STD_ARRAY
JOIN STANDARD_LIBRARY STD_ARRAY_SELECT
JOIN STANDARD_LIBRARY STD_BROWSE
JOIN STANDARD_LIBRARY STD_MESSAGE
JOIN STANDARD_LIBRARY STD_PROMPT
JOIN STANDARD_LIBRARY STD_STRUCTURE
JOIN STANDARD_LIBRARY STD_UTILS
JOIN STANDARD_LIBRARY STD_TRANSFER

JOIN LIBRARY $CRITERIA_EDIT
JOIN LIBRARY $LIB_UTILS
JOIN LIBRARY $LIST_BOX
JOIN LIBRARY $TABLE_LOAD_SAVE_LIB

ENABLE WINDOWS

{* CONSTANTS ******************************************************************}


{* VARIABLES ******************************************************************}



DECLARE global_table_names, tnames, basedir, cnt, ntab
ARRAY tnames

basedir = "c:\zip\dump\"
{select 'tnames['||row_number() over(order by table_name)||']="'||table_name||'"' from user_tables }

tnames[21]="BARMENU"
tnames[36]="BLOB_VALUES"
tnames[37]="BL_RELATIONSHIP"
tnames[38]="BL_RELATIONSHIP_CLAUSE"
tnames[39]="BL_TYPE"
tnames[40]="CALCULATION"
tnames[41]="CALC_FUNCTION"
tnames[42]="CALC_FUNCTION_PARAMETER"
tnames[51]="CGI_SCRIPT"
tnames[52]="CHAPTER"
tnames[66]="CLIENT_ACCESS"
tnames[70]="CONFIG_HEADER"
tnames[71]="CONFIG_ITEM"
tnames[75]="CONTENTS"
tnames[77]="CRITERIA_CONDITION"
tnames[79]="CRITERIA_ORDER"
tnames[80]="CRITERIA_SAVED"
tnames[81]="CRITERIA_VARIABLE"
tnames[108]="DM_AGENT_COMPUTER"
tnames[109]="DM_AGENT_STATUS"
tnames[110]="DM_CAPTURE_INTERVAL"
tnames[111]="DM_CAPTURE_SOURCE"
tnames[112]="DM_CLIENT_COMPUTER"
tnames[113]="DM_CONVERTER"
tnames[114]="DM_DATA_DESTINATION"
tnames[115]="DM_DATA_ITEM"
tnames[117]="DM_DATA_ITEM_LINK"
tnames[118]="DM_DATA_ITEM_PROPERTY"
tnames[119]="DM_DATA_PACKAGE"
tnames[120]="DM_DATA_PACKAGE_FILE"
tnames[121]="DM_EVENT_ENTRY"
tnames[129]="DM_GLOBAL"
tnames[130]="DM_PACKAGE_PROPERTY"
tnames[131]="DM_PACKAGING_RULE"
tnames[132]="DM_PROPERTY_SCRIPT"
tnames[133]="DM_PROPERTY_SCRIPT_ENTRY"
tnames[134]="DM_RULE_TEMPLATE"
tnames[135]="DM_SERVER_COMPUTER"
tnames[136]="DM_STATUS_AGENT"
tnames[137]="DM_STATUS_CAPTURE_SOURCE"
tnames[145]="ENTITY_TEMPLATE"
tnames[146]="ENTITY_TEMPLATE_FIELD"
tnames[148]="ENTITY_TEMPLATE_PROPERTY"
tnames[152]="EXPLORER_AUX"
tnames[153]="EXPLORER_AUX_FIELDS"
tnames[154]="EXPLORER_CABINET"
tnames[155]="EXPLORER_CABINET_ROLE"
tnames[156]="EXPLORER_COLUMN"
tnames[157]="EXPLORER_COLUMN_USER"
tnames[158]="EXPLORER_EXPRESSION"
tnames[159]="EXPLORER_FOLDER"
tnames[160]="EXPLORER_FOLDER_ICON"
tnames[161]="EXPLORER_FOLDER_LINK"
tnames[162]="EXPLORER_GROUP"
tnames[163]="EXPLORER_HIERARCHY"
tnames[164]="EXPLORER_HIERARCHY_LEVEL"
tnames[165]="EXPLORER_RMB"
tnames[166]="EXPLORER_ROLE"
tnames[168]="FORM"
tnames[169]="FORM_PAGE"
tnames[170]="GROUPLINK"
tnames[172]="GROUP_HEADER"
tnames[177]="HELP_CONTEXT"
tnames[178]="ICON"
tnames[181]="INCIDENT_TEMPLATE"
tnames[182]="INCREMENTS"
tnames[185]="INFOMAKER_LINK"
tnames[186]="INFOMAKER_PARAMETERS"
tnames[208]="INTERFACE"
tnames[209]="INTERFACE_ACTION"
tnames[210]="INTERFACE_FIELD"
tnames[214]="JOB_TEMPLATE"
tnames[215]="LABEL_TEMPLATE"
tnames[243]="LIMIT_CALCULATION"
tnames[246]="LIMSML_ENTITY_ACTION"
tnames[247]="LIMSML_ENTITY_PARAM"
tnames[248]="LIMS_TABLE"
tnames[250]="LIST_RESULT_FORMAT"
tnames[252]="LOCATION_TYPE"
tnames[253]="LOCATION_TYPE_LIST"
tnames[261]="MASTER_MENU"
tnames[262]="MENU"
tnames[263]="MENU_MYTASKS"
tnames[264]="MENU_TOOLBAR"
tnames[276]="MT_FIELDS"
tnames[281]="PARAMETER_TYPE"
tnames[282]="PASSWORD"
tnames[285]="PERSONNEL"
tnames[287]="PHRASE"
tnames[288]="PHRASE_HEADER"
tnames[306]="REMOTE_MODEL"
tnames[307]="REPORT"
tnames[314]="REPORT_LAYOUT_FIELD"
tnames[315]="REPORT_LAYOUT_HEADER"
tnames[316]="REPORT_LAYOUT_LINK"
tnames[318]="REPORT_TEMPLATE"
tnames[329]="RESULT"
tnames[330]="RESULT_FILE"
tnames[331]="ROLE_ASSIGNMENT"
tnames[332]="ROLE_ENTRY"
tnames[333]="ROLE_HEADER"
tnames[338]="SAMPLE"
tnames[345]="SAMPLE_POINT"
tnames[348]="SAMP_TMPL_HEADER"
tnames[360]="SMW_MENU"
tnames[363]="SQCWIN_CHART_TYPE"
tnames[365]="SQCWIN_HEADER"
tnames[366]="SQCWIN_POINT"
tnames[367]="SQCWIN_SUBGROUP"
tnames[368]="SQCWIN_TREND"
tnames[369]="SQCWIN_TREND_POINT"
tnames[372]="SQC_REMAP"
tnames[376]="STAT_ATRIB_DOUBLE"
tnames[377]="STAT_ATRIB_SINGLE"
tnames[378]="STAT_CODE"
tnames[380]="STAT_VARBL_SINGLE"
tnames[400]="SYNTAX"
tnames[401]="S_ANALYSIS_LOOKUP"
tnames[402]="S_PROPERTY_LOOKUP"
tnames[408]="TAB_OUTPUT_COLUMNS"
tnames[410]="TEMPLATE_FIELDS"
tnames[412]="TEST"
tnames[418]="TIMERQUEUE"
tnames[419]="TIMER_SCHEDULE"
tnames[425]="UNIT_ALIAS"
tnames[426]="UNIT_HEADER"
tnames[428]="USER_SETTING"
tnames[429]="VERSIONED_ANALYSIS"
tnames[432]="VERSIONED_CATEGORY_COMPONENT"
tnames[433]="VERSIONED_COMPONENT"
tnames[437]="WEB_LINK"
tnames[438]="WEB_MENU_ITEM"
tnames[439]="WEB_MENU_SECTION"
tnames[442]="WORKFLOW"
tnames[443]="WORKFLOW_ACTION_ROLE"
tnames[444]="WORKFLOW_ACTION_TYPE"
tnames[445]="WORKFLOW_EVENT_TYPE"
tnames[448]="WORKFLOW_NODE"
tnames[450]="WORKFLOW_STATE"
tnames[451]="WORKFLOW_STATE_CONDITION"

{******************************************************************************}

cnt = 1
ntab = SIZE_OF_ARRAY(tnames)

WHILE (cnt <= ntab) DO

    IF ( (tnames[cnt] != EMPTY) AND NOT BLANK(tnames[cnt]))  THEN
    
        batch_table_to_csv_file(tnames[cnt],basedir:tnames[cnt]:".csv")
    
    ENDIF
    
    cnt = cnt + 1

ENDWHILE

{* INITIALISATION ROUTINES ****************************************************}

{******************************************************************************}

    ROUTINE inter_initialise

{
*       Initialisation routine - install the list box class and read table
*   names
*
*******************************************************************************}

    set_up_list_box_class ( )

    ARRAY global_table_names ARRAYSIZE ( 0 , 2 )

    get_table_names ( global_table_names )

ENDROUTINE

{******************************************************************************}

    ROUTINE batch_initialise

{
*       Initialisation routine - do nothing
*
*******************************************************************************}

ENDROUTINE


{* GLOBAL ROUTINES ************************************************************}

{******************************************************************************}

    GLOBAL

    ROUTINE table_saver_specific_table ( VALUE passed_table_name )

{
*       Main entry point
*
*******************************************************************************}

    inter_initialise ( )

    RETURN ( inter_table_to_csv_file ( passed_table_name ))
    
ENDROUTINE

{* LOCAL ROUTINES *************************************************************}

    ROUTINE inter_table_to_csv_file ( VALUE passed_table_name )

{
*       The main save routine - prompts for all necessary information then
*   calls routines to save the data.
*
*******************************************************************************}

    DECLARE save_form , table_prompt , table_object , file_prompt ,
        file_object , field_select_prompt , field_select_object ,
        record_select_prompt , record_select_object , prompt_width ,
        can_exit , list_of_fields , table_name_len , selection_array ,
        mandatory_array , exit_key , real_exit , start_column ,
            next_line , cs_object , return_status,
            return_message

    return_status = FALSE

    { Create the form }

    CREATE OBJECT PROMPT_CLASS_FORM , save_form

    save_form . width            = 70
    save_form . row              = 11
    save_form . column           =
        ( ( GLOBAL ( "SCREEN_WIDTH" ) - 70 ) DIV 2 )
    save_form . border           = TRUE
    save_form . header           =
        GET_USER_MESSAGE ( "TABLE_SAVER_FORM_HEADER" , 1 )
    save_form . footer           =
        GET_USER_MESSAGE ( "TABLE_SAVER_FORM_FOOTER" , 1 )
    save_form . confirm_required = FALSE
    save_form . return_behaviour = FORM_RETURN_WRAP
    save_form . help_context     = "$TABLE_SAVER_FORM_PROMPT"
    save_form . prompt_id        = "$TABLE_SAVER_FORM_PROMPT"

    { Prepare the prompts }

    GET_FIELD_DETAILS criteria . table_name , "FIELD_SIZE" , table_name_len

    table_prompt         = GET_USER_MESSAGE ( "TABLE_SAVER_TABLE_PR" , 1 )
    file_prompt          = GET_USER_MESSAGE ( "TABLE_SAVER_FILE_PR" , 1 )
    field_select_prompt  = GET_USER_MESSAGE ( "TABLE_SAVER_SEL_FLD_PR" , 1 )
    record_select_prompt = GET_USER_MESSAGE ( "TABLE_SAVER_SEL_REC_PR" , 1 )

    prompt_width = find_max_number ( LENGTH ( table_prompt ) ,
                                     LENGTH ( file_prompt  ) )

    prompt_width = find_max_number ( prompt_width                   ,
                                     LENGTH ( field_select_prompt ) )

    prompt_width = find_max_number ( prompt_width                    ,
                                     LENGTH ( record_select_prompt ) )


    table_prompt         = PAD ( table_prompt , " " , prompt_width )
    file_prompt          = PAD ( file_prompt  , " " , prompt_width )
    field_select_prompt  = PAD ( field_select_prompt , " " , prompt_width )
    record_select_prompt = PAD ( record_select_prompt , " " , prompt_width )

    IF global ( "TERMTYPE" ) = "GUI" THEN
        save_form . height = 7
        prompt_width = prompt_width + 1
        start_column = 2
    ELSE
        save_form . height   = 5
        prompt_width         = prompt_width + 3
        start_column         = 1
        table_prompt         = table_prompt : " : "
        file_prompt          = file_prompt : " : "
        field_select_prompt  = field_select_prompt : " : "
        record_select_prompt = record_select_prompt : " : "
    ENDIF

    save_form . add_display ( table_prompt            ,
                              start_column            ,
                              1                       ,
                              PROMPT_RENDITION_RAISED +
                              PROMPT_RENDITION_BOLD   )

    IF blank ( passed_table_name ) THEN

        PROMPT OBJECT table_object
                CHOOSE OUTOF global_table_names
            ON LINE 1 FROM ( start_column + prompt_width )
            WITH ( always_validate = TRUE,
                       leave_prompt_routine = "TABLE_LEAVE_ROUTINE",
                       vgl_library = GLOBAL ( "CURRENT_LIBRARY" ))


    ELSE

        PROMPT OBJECT table_object
                BROWSE ON DISPLAY
            ON LINE 1 FROM ( start_column + prompt_width )
            WITH ( value = passed_table_name )

    ENDIF

    save_form . add_prompt ( table_object )

    IF global ( "TERMTYPE" ) = "GUI" THEN

        save_form . add_display ( file_prompt           ,
                                  start_column          ,
                                  3                     ,
                                  PROMPT_RENDITION_RAISED +
                                  PROMPT_RENDITION_BOLD   )

        PROMPT OBJECT file_object
            FORMAT FILE
            ON LINE 3
            FROM ( start_column + prompt_width )
            TO save_form . width - 3
            WITH ( file_directory  = DEFAULT_CLIENT_CSV_FILE_DIR ,
                       file_extension  = DEFAULT_CLIENT_CSV_FILE_EXTN,
                       client_file     = TRUE                        ,
                       may_browse      = FALSE                       ,
                       user_info       = TRUE                        )

        PROMPT OBJECT cs_object
            BROWSE ON BOOLEAN
            ON LINE 2 FROM ( start_column + prompt_width )
            WITH ( toggled_routine = "CLIENT_TOGGLED"            ,
                   value           = TRUE                        ,
                       vgl_library     = GLOBAL ( "CURRENT_LIBRARY" ),
                       user_info       = file_object                 ,
                       is_check        = TRUE                        ,
                       caption = GET_USER_MESSAGE ( "TABLE_LOADER_CLIENT_PR" , 1 ))

        save_form . add_prompt ( cs_object )
        save_form . add_prompt ( file_object )

        save_form . add_frame ( "", 1, 1, 3, save_form . width )

        next_line = 6

    ELSE

        save_form . add_display ( file_prompt           ,
                                  start_column          ,
                                  2                     ,
                                  PROMPT_RENDITION_RAISED +
                                  PROMPT_RENDITION_BOLD   )

        PROMPT OBJECT file_object
            FORMAT FILE
            ON LINE 2
            FROM ( start_column + prompt_width )
            TO save_form . width
            WITH ( file_directory = DEFAULT_CSV_FILE_DIR  ,
                   file_extension = DEFAULT_CSV_FILE_EXTN ,
                       client_file    = FALSE                 ,
                       may_browse     = FALSE                 ,
                       user_info      = FALSE                 )

        save_form . add_prompt ( file_object )

        next_line = 4

    ENDIF

    save_form . add_display ( field_select_prompt     ,
                              start_column            ,
                              next_line               ,
                              PROMPT_RENDITION_RAISED +
                              PROMPT_RENDITION_BOLD   )

    PROMPT OBJECT field_select_object
        BROWSE ON BOOLEAN
        ON LINE next_line FROM ( start_column + prompt_width )
        WITH
         ( true_word  = GET_USER_MESSAGE ( "TABLE_SAVER_SEL_FLD" , 1 ) ,
           false_word = GET_USER_MESSAGE ( "TABLE_SAVER_ALL_FLD" , 1 ) )

    field_select_object  . value = FALSE

    save_form . add_prompt ( field_select_object )

    save_form . add_display ( record_select_prompt    ,
                              start_column            ,
                              next_line + 1           ,
                              PROMPT_RENDITION_RAISED +
                              PROMPT_RENDITION_BOLD   )

    PROMPT OBJECT record_select_object
        BROWSE ON BOOLEAN
        ON LINE next_line + 1 FROM ( start_column + prompt_width )
        WITH
         ( true_word  = GET_USER_MESSAGE ( "TABLE_SAVER_SEL_REC" , 1 ) ,
           false_word = GET_USER_MESSAGE ( "TABLE_SAVER_ALL_REC" , 1 ) )

    record_select_object . value = FALSE

    save_form . add_prompt ( record_select_object )

    save_form . add_frame ( "", 1, next_line,
                                2, save_form . width )

    REPEAT
        save_form . start_prompt ( )

        REPEAT
            save_form . wait_prompt ( )

            IF ( save_form . get_lastkey ( ) = "EXIT" ) THEN

                can_exit = TRUE

            ELSEIF BLANK ( table_object . text ) THEN

                save_form . active_prompt = 1
                can_exit = FALSE

            ELSEIF BLANK ( file_object . value ) THEN

                IF global ( "TERMTYPE" ) = "GUI" THEN
                    save_form . active_prompt = 3
                ELSE
                    save_form . active_prompt = 2
                ENDIF
                can_exit = FALSE
            ELSE
                IF ( field_select_object . value ) OR
                   ( record_select_object . value ) THEN

                    can_exit = TRUE
                ELSE

                    can_exit = confirm ( )
                ENDIF
            ENDIF

        UNTIL can_exit

        exit_key = save_form . get_lastkey ( )

        save_form . end_prompt ( )

        IF ( exit_key = "EXIT" ) THEN

            real_exit = TRUE

        ELSE
            real_exit = FALSE

            ARRAY list_of_fields ARRAYSIZE ( 0 )

            get_field_names_without_aliases ( table_object . text ,
                                              list_of_fields      )

            IF ( field_select_object . value ) THEN

                get_field_selection ( table_object . text ,
                                      list_of_fields      )

                exit_key = LASTKEY
            ENDIF

            IF ( exit_key <> "EXIT" ) AND
               ( size_of_array ( list_of_fields ) > 0 ) THEN

                ARRAY selection_array

                IF ( record_select_object . value ) THEN

                    ARRAY mandatory_array

                    SET CONFIRM FALSE

                    criteria_edit ( table_object . text ,
                                    selection_array     ,
                                    mandatory_array     ,
                                    TRUE                ,
                                    TRUE                )

                    SET CONFIRM TRUE

                ENDIF

                IF ( exit_key <> "EXIT" ) THEN

                    DECLARE temp_name, status

                    IF file_object . user_info THEN
                        temp_name = strip (
                                     file_object.value)
                        file_object.value =
                               "smp$userfiles:temp" :
                               global ( "PROCESS_ID" ) :
                               ".csv"
                    ENDIF

                    return_status = output_csv_file( STRIP( file_object . value ) ,
                                                     table_object . text          ,
                                                     list_of_fields               ,
                                                     selection_array              ,
                                                     TRUE                         ,
                                                     return_message               )

                    IF file_object . user_info THEN
                        transfer_from_server (
                                      file_object.value,
                                          temp_name )
                        FILE DELETE file_object . value,
                                    status
                        file_object . value = temp_name
                    ENDIF
                    
                    real_exit = NOT BLANK ( passed_table_name )
                    
                ENDIF
            ENDIF
        ENDIF

    UNTIL real_exit
    
    RETURN ( return_status )

ENDROUTINE

{******************************************************************************}

    ROUTINE get_field_selection ( VALUE name_of_table     ,
                                        full_field_array  )

{
*       Allows the user to edit a list of fields.
*
*******************************************************************************}

    DECLARE field_list_box , selection_list , size_of_list , mess_ptr ,
        count

    CREATE OBJECT "STD_LIST_BOX" , field_list_box

    size_of_list = size_of_array ( full_field_array )

    ARRAY selection_list ARRAYSIZE ( size_of_list )

    count = 0

    WHILE count < size_of_list DO

        count = count + 1
        selection_list [ count ] = TRUE

    ENDWHILE

    message_fetch ( "TABLE_SAVER_FIELD_LIST" , mess_ptr )
    message_add_parameter ( mess_ptr , STRIP ( name_of_table ) )

    field_list_box . display_list     = full_field_array
    field_list_box . select_list      = selection_list
    field_list_box . width            = 60
    field_list_box . height           = 10
    field_list_box . row              =  9
    field_list_box . column           =
        ( GLOBAL ( "SCREEN_WIDTH" ) - 60 ) DIV 2
    field_list_box . header           =
        GET_USER_MESSAGE ( "TABLE_SAVER_LIST_HEADER" , 1 )
    field_list_box . list_title       = message_get_text ( mess_ptr , 1 )
    field_list_box . footer           =
        GET_USER_MESSAGE ( "TABLE_SAVER_LIST_FOOTER" , 1 )
    field_list_box . help_context     = "$TABLE_LOAD_FIELD_LIST"
    field_list_box . confirm_required = FALSE

    field_list_box . start_prompt ( )
    field_list_box . wait_prompt ( )
    field_list_box . end_prompt ( )

    IF ( LASTKEY <> "EXIT" ) THEN

        count = size_of_list

        WHILE count > 0 DO

            IF ( NOT selection_list [ count ] ) THEN

                array_remove_slice ( full_field_array ,
                                     1                ,
                                     count            )
            ENDIF

            count = count - 1

        ENDWHILE
    ENDIF

ENDROUTINE

{******************************************************************************}

    ROUTINE table_leave_routine ( self )

{
*       Table Name Leave Prompt Routine
*
*******************************************************************************}

    DECLARE file_prompt, file_name, pointer

    pointer = self . tag + 1
    IF GLOBAL ( "TERMTYPE" ) = "GUI" THEN
        pointer = self . tag + 2
    ENDIF

    file_prompt = self . parent_prompt . prompt_objects [ pointer ]

    file_name = STRIP ( file_prompt . file_directory ) :
                STRIP ( self . text ) : "." :
                STRIP ( file_prompt . file_extension )
    file_name = TOLOWER ( file_name )
    
    file_prompt . set_text ( file_name )

ENDROUTINE { table_leave_routine }

{******************************************************************************}

    ROUTINE client_toggled ( self )

{
*       Client Toggle Routine
*
*******************************************************************************}

    DECLARE table_prompt, name_prompt

    name_prompt  = self . parent_prompt . prompt_objects [ self . tag + 1 ]
    table_prompt = self . parent_prompt . prompt_objects [ self . tag - 1 ]

    IF self . value THEN

        name_prompt . file_directory = DEFAULT_CLIENT_CSV_FILE_DIR
        name_prompt . file_extension = DEFAULT_CLIENT_CSV_FILE_EXTN
        self . user_info . user_info = TRUE

    ELSE
        
        name_prompt . file_directory = DEFAULT_CSV_FILE_DIR
        name_prompt . file_extension = DEFAULT_CSV_FILE_EXTN
        self . user_info . user_info = FALSE

    ENDIF

    table_prompt = self . parent_prompt . prompt_objects [ self . tag - 1 ]
    table_leave_routine ( table_prompt )

ENDROUTINE { client_toggled }

{******************************************************************************}

    ROUTINE batch_table_to_csv_file (VALUE name_of_table, VALUE file_name)

{
*       The main batch save routine - reads for all necessary information then
*   calls routines to save the data.
*
*******************************************************************************}

    DECLARE error_message , error_occured ,
        mess_ptr , list_of_fields , selection_array , return_message

    error_occured = FALSE


    IF ( NOT error_occured ) THEN

        IF ( NOT valid_table ( name_of_table ) ) THEN

            message_fetch ( "TABLE_SAVER_ILL_TBL_NAME" , mess_ptr )
            message_add_parameter ( mess_ptr , name_of_table )
            error_message = message_get_text ( mess_ptr , 1 )
            error_occured = TRUE
        ENDIF
    ENDIF

    IF ( NOT error_occured ) THEN

        ARRAY list_of_fields ARRAYSIZE ( 0 )

        get_field_names_without_aliases ( name_of_table  ,
                                          list_of_fields )

        IF ( size_of_array ( list_of_fields ) > 0 ) THEN

            ARRAY selection_array

            { Don't get removed records }
            IF (valid_field(name_of_table,"REMOVEFLAG")) THEN
            
                array_select_add(selection_array,ARRAY_SELECT_EQ,"REMOVEFLAG",FALSE)
                
            ENDIF
           
            output_csv_file ( STRIP ( file_name )     ,
                              STRIP ( name_of_table ) ,
                              list_of_fields          ,
                              selection_array         ,
                      FALSE                   ,
                              return_message          )
        ENDIF

    ELSE
        send_message ( error_message , TABLE_SAVER , FALSE )
        send_message ( "TABLE_SAVER_ABORTED" , TABLE_SAVER , FALSE )
    ENDIF

ENDROUTINE


